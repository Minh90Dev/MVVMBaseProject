plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
}

android {
    namespace 'com.yadokari.tv.mvvmbase'
    compileSdk 34

    defaultConfig {
        applicationId "com.yadokari.tv.mvvmbase"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            multiDexEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {}
    }

    flavorDimensions = ["flavors"]
    productFlavors {
        develop {
            applicationId "com.yadokari.tv.mvvmbase.dev"
            resValue "string", "app_name", "Base Project (dev)"
            buildConfigField("String", "API_BASE_URL", "\"${DEVELOP_API_BASE_URL}\"")
        }

        staging {
            applicationId "com.yadokari.tv.mvvmbase.stg"
            resValue "string", "app_name", "Base Project (stg)"
            buildConfigField("String", "API_BASE_URL", "\"${STAGING_API_BASE_URL}\"")
        }

        product {
            applicationId "com.yadokari.tv.mvvmbase"
            resValue "string", "app_name", "Base Project"
            buildConfigField("String", "API_BASE_URL", "\"${PRODUCTION_API_BASE_URL}\"")
        }
    }

    viewBinding {
        enabled = true
    }

    buildFeatures {
        buildConfig = true
        dataBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.multidex:multidex:2.0.1'

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_view_model_version"

    // UI
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.10.0'

    // REST - APIService
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$converter_moshi_version"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    ksp "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    // Koin
    implementation platform("io.insert-koin:koin-bom:$koin_version")
    implementation "io.insert-koin:koin-core"
    implementation "io.insert-koin:koin-android"
    testImplementation "io.insert-koin:koin-test"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Logger
    implementation('com.github.ihsanbal:LoggingInterceptor:3.1.0') {
        exclude group: 'org.json', module: 'json'
    }

    // Test
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.koin:koin-test:$koin_version"
    testImplementation "io.mockk:mockk:$mockk_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}